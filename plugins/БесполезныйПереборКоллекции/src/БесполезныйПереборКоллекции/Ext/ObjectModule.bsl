
Перем Типы;
Перем ТаблицаОшибок;
Перем Стек;

Перем ИспользуемыеПеременные;

Процедура Открыть(Парсер, Параметры) Экспорт
	
	Типы = Парсер.Типы();
	ТаблицаОшибок = Парсер.ТаблицаОшибок();
	Стек = Парсер.Стек();
	
	ИспользуемыеПеременные = Новый Соответствие;
	
КонецПроцедуры

Функция Закрыть() Экспорт
	
	ИспользуемыеПеременные = Неопределено;
	
	Возврат Неопределено;
	
КонецФункции

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьОператорДляКаждого");
	Подписки.Добавить("ПокинутьОператорДляКаждого");
	Подписки.Добавить("ПосетитьВыражениеИдентификатор");
	Возврат Подписки;
КонецФункции

#Область РеализацияПодписок

Процедура ПосетитьОператорДляКаждого(ОператорДляКаждого) Экспорт 
	
	ИспользуемыеПеременные[ОператорДляКаждого.Идентификатор.Голова] = Ложь;
		
КонецПроцедуры

Процедура ПосетитьВыражениеИдентификатор(ВыражениеИдентификатор) Экспорт 
	
	Родитель = Стек[Стек.ВГраница()];
	Если Родитель.Тип <> Типы.ОператорДляКаждого
		Или Родитель.Идентификатор <> ВыражениеИдентификатор Тогда
		ИспользуемыеПеременные[ВыражениеИдентификатор.Голова] = Истина;
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПокинутьОператорДляКаждого(ОператорДляКаждого) Экспорт
	
	Если Не ИспользуемыеПеременные[ОператорДляКаждого.Идентификатор.Голова] Тогда
		Ошибка("Бесполезный перебор коллекции", ОператорДляКаждого.Начало, ОператорДляКаждого.Конец);	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

Процедура Ошибка(Текст, Начало, Конец = Неопределено, ЕстьЗамена = Ложь)
	
	Ошибка = ТаблицаОшибок.Добавить();
	Ошибка.Источник = Метаданные().Имя;
	Ошибка.Текст = Текст;
	
	Ошибка.МинутНаИсправление = 5;
	Ошибка.Серьезность = "INFO";
	Ошибка.Приоритет = 0;
	Ошибка.Правило = Метаданные().Синоним;
	Ошибка.Тип = "CODE_SMELL";
	
	Ошибка.ПозицияНачала = Начало.Позиция;
	Ошибка.НомерСтрокиНачала = Начало.НомерСтроки;
	Ошибка.НомерКолонкиНачала = Начало.НомерКолонки;
	Если Конец = Неопределено Или Конец = Начало Тогда
		Ошибка.ПозицияКонца = Начало.Позиция + Начало.Длина;
		Ошибка.НомерСтрокиКонца = Начало.НомерСтроки;
		Ошибка.НомерКолонкиКонца = Начало.НомерКолонки + Начало.Длина;
	Иначе
		Ошибка.ПозицияКонца = Конец.Позиция + Конец.Длина;
		Ошибка.НомерСтрокиКонца = Конец.НомерСтроки;
		Ошибка.НомерКолонкиКонца = Конец.НомерКолонки + Конец.Длина;
	КонецЕсли;
	
	Ошибка.ЕстьЗамена = ЕстьЗамена;
	
КонецПроцедуры
