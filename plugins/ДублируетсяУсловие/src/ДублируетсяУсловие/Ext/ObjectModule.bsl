
Перем Исходник;
Перем ТаблицаОшибок;

Процедура Открыть(Парсер, Параметры) Экспорт
	Исходник = Парсер.Исходник();
	ТаблицаОшибок = Парсер.ТаблицаОшибок();
КонецПроцедуры

Функция Закрыть() Экспорт
	Возврат Неопределено;
КонецФункции

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьОператорЕсли");
	Возврат Подписки;
КонецФункции

#Область РеализацияПодписок

Процедура ПосетитьОператорЕсли(ОператорЕсли) Экспорт
	Если ОператорЕсли.ИначеЕсли <> Неопределено Тогда
		НачалоВыражения = ОператорЕсли.Выражение.Начало;
		КонецВыражения = ОператорЕсли.Выражение.Конец;
		ДлинаВыражения = КонецВыражения.Позиция + КонецВыражения.Длина - НачалоВыражения.Позиция;
		УникальныеУсловия = Новый Соответствие;
		УникальныеУсловия[Сред(Исходник, НачалоВыражения.Позиция, ДлинаВыражения)] = Истина;  
		Для Каждого ОператорИначеЕсли Из ОператорЕсли.ИначеЕсли Цикл
			НачалоВыражения = ОператорИначеЕсли.Выражение.Начало;
			КонецВыражения = ОператорИначеЕсли.Выражение.Конец;
			ДлинаВыражения = КонецВыражения.Позиция + КонецВыражения.Длина - НачалоВыражения.Позиция;
			ТекстУсловия = Сред(Исходник, НачалоВыражения.Позиция, ДлинаВыражения);
			Если УникальныеУсловия[ТекстУсловия] <> Неопределено Тогда
				Ошибка("Дублируется условие", НачалоВыражения, КонецВыражения);
			Иначе
				УникальныеУсловия[ТекстУсловия] = Истина;
			КонецЕсли;	 
		КонецЦикла; 
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти // РеализацияПодписок

Процедура Ошибка(Текст, Начало, Конец = Неопределено, ЕстьЗамена = Ложь)
	
	Ошибка = ТаблицаОшибок.Добавить();
	Ошибка.Источник = Метаданные().Имя;
	Ошибка.Текст = Текст;
	
	Ошибка.МинутНаИсправление = 5;
	Ошибка.Серьезность = "INFO";
	Ошибка.Приоритет = 0;
	Ошибка.Правило = Метаданные().Синоним;
	Ошибка.Тип = "CODE_SMELL";
	
	Ошибка.ПозицияНачала = Начало.Позиция;
	Ошибка.НомерСтрокиНачала = Начало.НомерСтроки;
	Ошибка.НомерКолонкиНачала = Начало.НомерКолонки;
	Если Конец = Неопределено Или Конец = Начало Тогда
		Ошибка.ПозицияКонца = Начало.Позиция + Начало.Длина;
		Ошибка.НомерСтрокиКонца = Начало.НомерСтроки;
		Ошибка.НомерКолонкиКонца = Начало.НомерКолонки + Начало.Длина;
	Иначе
		Ошибка.ПозицияКонца = Конец.Позиция + Конец.Длина;
		Ошибка.НомерСтрокиКонца = Конец.НомерСтроки;
		Ошибка.НомерКолонкиКонца = Конец.НомерКолонки + Конец.Длина;
	КонецЕсли;
	
	Ошибка.ЕстьЗамена = ЕстьЗамена;
	
КонецПроцедуры
