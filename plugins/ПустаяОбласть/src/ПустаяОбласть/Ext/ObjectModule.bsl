
Перем ТаблицаОшибок;
Перем ТаблицаТокенов;
Перем Токены;
Перем Типы;

Перем СтекОбластей;

Процедура Открыть(Парсер, Параметры) Экспорт
	
	ТаблицаОшибок = Парсер.ТаблицаОшибок();
	ТаблицаТокенов = Парсер.ТаблицаТокенов();
	Токены = Парсер.Токены();
	Типы = Парсер.Типы();
	
	СтекОбластей = Новый Массив;
	
КонецПроцедуры

Функция Закрыть() Экспорт
	СтекОбластей = Неопределено;
	Возврат Неопределено;
КонецФункции

Функция Подписки() Экспорт
	Перем Подписки;
	Подписки = Новый Массив;
	Подписки.Добавить("ПосетитьИнструкциюПрепроцессора");
	Возврат Подписки;
КонецФункции

#Область РеализацияПодписок


Процедура ПосетитьИнструкциюПрепроцессора(ИнструкцияПрепроцессора) Экспорт
	
	Если ИнструкцияПрепроцессора.Тип = Типы.ИнструкцияПрепроцессораОбласть Тогда
		
		СтекОбластей.Добавить(ИнструкцияПрепроцессора);
		
	ИначеЕсли ИнструкцияПрепроцессора.Тип = Типы.ИнструкцияПрепроцессораКонецОбласти Тогда 
		
		КонецОбласти = ИнструкцияПрепроцессора;
		
		Если СтекОбластей.Количество() > 0 Тогда
			
			Область = СтекОбластей[СтекОбластей.ВГраница()];
			СтекОбластей.Удалить(СтекОбластей.ВГраница());
			
			НетЗначимыхТокенов = Истина;
			
			Для Индекс = Область.Конец.Индекс + 1 По КонецОбласти.Начало.Индекс - 1 Цикл
				Если ТаблицаТокенов[Индекс].Токен <> Токены.Комментарий Тогда
					НетЗначимыхТокенов = Ложь;
					Прервать;
				КонецЕсли; 
			КонецЦикла; 
			
			Если НетЗначимыхТокенов Тогда
				Ошибка("Пустая область", Область.Начало, КонецОбласти.Конец);
			КонецЕсли; 
			
		КонецЕсли;
	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти // РеализацияПодписок

Процедура Ошибка(Текст, Начало, Конец = Неопределено, ЕстьЗамена = Ложь)
	
	Ошибка = ТаблицаОшибок.Добавить();
	Ошибка.Источник = Метаданные().Имя;
	Ошибка.Текст = Текст;
	
	Ошибка.МинутНаИсправление = 5;
	Ошибка.Серьезность = "INFO";
	Ошибка.Приоритет = 0;
	Ошибка.Правило = Метаданные().Синоним;
	Ошибка.Тип = "CODE_SMELL";
	
	Ошибка.ПозицияНачала = Начало.Позиция;
	Ошибка.НомерСтрокиНачала = Начало.НомерСтроки;
	Ошибка.НомерКолонкиНачала = Начало.НомерКолонки;
	Если Конец = Неопределено Или Конец = Начало Тогда
		Ошибка.ПозицияКонца = Начало.Позиция + Начало.Длина;
		Ошибка.НомерСтрокиКонца = Начало.НомерСтроки;
		Ошибка.НомерКолонкиКонца = Начало.НомерКолонки + Начало.Длина;
	Иначе
		Ошибка.ПозицияКонца = Конец.Позиция + Конец.Длина;
		Ошибка.НомерСтрокиКонца = Конец.НомерСтроки;
		Ошибка.НомерКолонкиКонца = Конец.НомерКолонки + Конец.Длина;
	КонецЕсли;
	
	Ошибка.ЕстьЗамена = ЕстьЗамена;
	
КонецПроцедуры
