
Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
	
		Старт = ТекущаяУниверсальнаяДатаВМиллисекундах();
				
		Посетитель = Обработки.Радар_Посетитель.Создать();
		Посетитель.ПосетитьКонфигурацию(ЭтотОбъект);
		Модули = Посетитель.Модули;
		ГлобальныеСвойства = Посетитель.ГлобальныеСвойства;
		ГлобальныеМетоды = Посетитель.ГлобальныеМетоды;
		Посетитель = Неопределено;
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Период", ТекущаяДатаСеанса());
		Запрос.УстановитьПараметр("Проект", ЭтотОбъект.Проект);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Радар_ТрендОстатки.Модуль КАК Модуль,
		|	Радар_ТрендОстатки.КоличествоОшибокОстаток КАК КоличествоОшибок
		|ПОМЕСТИТЬ Остатки
		|ИЗ
		|	РегистрНакопления.Радар_Тренд.Остатки(, Проект = &Проект) КАК Радар_ТрендОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Модуль";
		
		Запрос.Выполнить();
		
		Если Радар_КлиентСервер.ЭтоФайловаяБаза() Или Не ЭтотОбъект.Проект.ИспользоватьМногопоточность Тогда
			Радар_Задания.РазобратьПартиюМодулей(Модули, ЭтотОбъект.Ссылка, ЭтотОбъект.Проект, ГлобальныеСвойства, ГлобальныеМетоды);
		Иначе
			Радар_Задания.РазобратьМодули(Модули, ЭтотОбъект.Ссылка, ЭтотОбъект.Проект, ГлобальныеСвойства, ГлобальныеМетоды);
		КонецЕсли; 
		
		Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Ссылка);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Радар_Проверки.Период КАК Период,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(Остатки.КоличествоОшибок, 0) < Радар_Проверки.КоличествоОшибок
		|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|	КОНЕЦ КАК ВидДвижения,
		|	Радар_Проверки.Проект КАК Проект,
		|	Радар_Проверки.Модуль КАК Модуль,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(Остатки.КоличествоОшибок, 0) < Радар_Проверки.КоличествоОшибок
		|			ТОГДА Радар_Проверки.КоличествоОшибок - ЕСТЬNULL(Остатки.КоличествоОшибок, 0)
		|		ИНАЧЕ ЕСТЬNULL(Остатки.КоличествоОшибок, 0) - Радар_Проверки.КоличествоОшибок
		|	КОНЕЦ КАК КоличествоОшибок
		|ИЗ
		|	РегистрСведений.Радар_Проверки КАК Радар_Проверки
		|		ЛЕВОЕ СОЕДИНЕНИЕ Остатки КАК Остатки
		|		ПО Радар_Проверки.Модуль = Остатки.Модуль
		|ГДЕ
		|	Радар_Проверки.Регистратор = &Регистратор
		|	И ВЫБОР
		|			КОГДА ЕСТЬNULL(Остатки.КоличествоОшибок, 0) < Радар_Проверки.КоличествоОшибок
		|				ТОГДА Радар_Проверки.КоличествоОшибок - ЕСТЬNULL(Остатки.КоличествоОшибок, 0)
		|			ИНАЧЕ ЕСТЬNULL(Остатки.КоличествоОшибок, 0) - Радар_Проверки.КоличествоОшибок
		|		КОНЕЦ > 0";
		
		Ошибки = Запрос.Выполнить().Выгрузить();
		
		ЭтотОбъект.Движения.Радар_Тренд.Загрузить(Ошибки);
		ЭтотОбъект.Движения.Радар_Тренд.Записать(Ложь);
		
		Финиш = ТекущаяУниверсальнаяДатаВМиллисекундах() - Старт;
		Сообщить(Финиш / 1000);
		
		ОбновитьПовторноИспользуемыеЗначения();
	
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("ЭтоНовый");
	КонецЕсли; 
	
КонецПроцедуры
